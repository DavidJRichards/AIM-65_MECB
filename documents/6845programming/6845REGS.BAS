
100 REM ** CONSTANTS **
105 DIM R(15)
110 R(3)=8
120 K$="REGISTER"
130 L$="MICROSECONDS"
150 REM ******** R0 ********
160 PRINT "HORIZONTAL LINE LENGTH (CHAR.); "
170 INPUT A0
180 R(0)=A0-1
190 TC=64/A0
200 FX=8/TC
210 PRINT "FREQUENCY = ";FX;" MHZ" 
220 PRINT "CRYSTAL FREQUENCY (MHZ) : "
230 INPUT FX
240 TC=1/(FX/8)
250 LPB=R(3)*TC
260 TSL=A0*TC
300 REM ******** R1 ********
310 PRINT "NUMBER OF CHARACTERS PER LINE:"
320 INPUT R(1) 
330 DT=R(1)*TC
400 REM ******** R2 ********
410 HP=DT+(TSL-1.5*LPB-DT)/2
420 R(2)=HP/TC
500 REM ******** R3 ********
600 REM ******** R4 ********
610 PRINT "NUMBER OF SCAN LINES: "
620 INPUT A
623 IF A<8 THEN PRINT "MINIMUM 8 SCAN LINES !":GOTO 610
625 PRINT "NUMBER OF CHARACTER LINES: "
630 INPUT B 
640 TR=(A)*TSL
650 VT=(B+1)*TR
660 IF VT<=20000 THEN 680
665 PRINT
670 PRINT " IMPOSSIBLE!"
675 PRINT "FEWER CHARACTER OR SCAN LINES. PLEASE. "
677 GOTO 600
680 Y=INT(20000/TR)
690 R(4)=Y-1
700 REM ******** R5 ********
710 R(5)=INT((20000-Y*TR)/TSL)
800 REM ******** R6 ********
810 R(6)=B
815 VD=R(6)*TR
900 REM ******** R7 ********
910 R(7)=INT ( ( ( (TR*Y+TSL*R(5) ) -( 1500+B*TR) )/2+B*TR)/TR) 
915 VP=R(7)*TR
1000 REM ******** R8 ********
1010 R(8)=0
1100 REM ******** R9 ********
1110 R(9)=A-1
1200 REM ******** R10 & R11 ********
1202 REM UNDERLINE CURSOR
1204 IF A=8 THEN R(11)=A :R(10)=64+A :GOTO 1300
1206 R(10)=73 :R(11)=9
1300 REM ******** R12. R13. R14 k R15 ********
1310 R(12)=0
1320 R(13)=0
1330 R(14)=0
1340 R(15)=0
1350 PRINT: PRINT
1352 PRINT "SCREEN FORMAT = ";R(1);" * ";B
1354 PRINT: PRINT
1700 FOR Q=0 TO 15
1710 PRINT K$;" R";Q;
1720 PRINT TAB(20);" = ";
1727 Z2=R(Q)
1730 GOSUB 2000
1740 PRINT
1750 NEXT Q
1760 PRINT : PRINT:
1770 BREAK :REM INPUT A$
1800 PRINT " CLOCK PERIOD                 ";TC;L$
1810 PRINT " LINE SYNC. PULSE WIDTH       ";LPB;L$
1815 PRINT " LINE SYNC. PULSE PERIOD      ";TSL;L$
1830 PRINT " HORIZONTAL DISPLAY TIME      ";DT;L$
1840 PRINT " HORIZONTAL POSITION          ";HP;L$
1850 PRINT " CHARACTER LINE PERIOD        ";TR;L$
1855 VE=Y*TR+R(5)*TSL
1860 PRINT " RASTER SYNC. PERIOD          ";VE;L$
1865 PRINT " VERTICAL DISPLAY TIME        ";VD;L$
1867 PRINT " VERTICAL POSITION            ";VP;L$
1990 END
2000 REM ******** DEC TO HEX ********
2001 REM PRINT Z2;" ";
2002 REM RETURN
2010 PRINT "$";
2020 FOR Z=1 TO 0 STEP -1 
2030 Z1=INT(Z2/16^Z)
2040 Z2=Z2-Z1*16^Z
2050 Z1=Z1+48
2060 IF Z1>57 THEN Z1=Z1+7
2070 PRINT CHR$(Z1);
2080 NEXT Z: RETURN


